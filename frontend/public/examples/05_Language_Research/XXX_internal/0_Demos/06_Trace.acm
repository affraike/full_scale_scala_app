
model Trace(pzero,D) = 
initially
  p   =  (0,0,0) ,
  p0  =  pzero ,
  p1  =  (0,0,0) ,
  t   =  0 ,
  t'  =  0 ,
  mode  =  "One" 
always
  t' = 1 ,
  match mode with [
    "One" ->   
      if t>0.1 then
        if norm(p0-p)>0 then 
          create cylinder(p0,p,D)
        noelse ,
        mode + = "Two"
      noelse
  | "Two" ->   
     if t>0.15 then
       mode + = "Three"
     noelse
  | "Three" ->   
      if t>0.12 then
        p0 + = p ,
        mode + = "One" ,
        t + = 0 
      noelse ]

model Main(simulator) = 
initially
  t  = 0 , t' = 0 ,
  a  =  create Trace((0,0,0),(0,0,0))
always

  t' = 2 ,

  a.p = (sqrt(t/4)*sin(t),sqrt(t/4)*cos(t),-t/50) 


model cylinder (p,q,D) = 
initially 
  //  p  = (0,0,0) , q = (0,0,0) ,
  _3D  =  () ,
  r  =  0.01 ,
  l  =  0.01 , alpha = 0 , theta =  pi/2 ,
  x = 0 ,y = 0 ,z = 0
always

  _3D = (Cylinder center=(p+q)/2+D radius=r length=l color=(0.2,0.2,0.2) 
          rotation=(alpha,0,-theta)) ,
  x = (p-q) dot (1,0,0) ,
  y = (p-q) dot (0,1,0) ,
  z = (p-q) dot (0,0,1) ,

  l = norm(p-q) ,
  alpha = asin(z/l) ,

  if y>0 then
   theta = asin(x/(l*cos(alpha)))
  else
   theta = -asin(x/(l*cos(alpha)))+pi
 
